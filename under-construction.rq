#+ summary: Number of houses under construction per municipality in year 1879
#+ tags:
#+   - houses
 
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX cedar: <http://lod.cedar-project.nl/vocab/cedar#>
PREFIX sdmx-dimension: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX sdmx-code: <http://purl.org/linked-data/sdmx/2009/code#>
 
SELECT ?municipality (SUM(?pop) AS ?tot)
FROM <urn:graph:cedar-mini:release>
WHERE { ?obs a qb:Observation . 
        ?obs cedar:population ?pop . 
        ?obs sdmx-dimension:refArea ?municipality .
        ?obs cedar:houseType <http://lod.cedar-project.nl/vocab/cedar#house-HuizenAanbouw> .        
        ?slice a qb:Slice.
	?slice qb:observation ?obs.
	?slice sdmx-dimension:refPeriod 1879 .
        FILTER (NOT EXISTS {?obs cedar:isTotal ?total }) .
} GROUP BY ?municipality ORDER BY ?municipality

